plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.2'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'com.diffplug.spotless' version '6.15.0'
}

allprojects { proj ->
	group = 'com.klindziuk.sas'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '17'
	repositories {
		mavenCentral()
	}
}

bootJar {
	enabled = false
}

jar {
	enabled = true
}

bootBuildImage.enabled = false
test.enabled = false

subprojects { sub ->
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'com.diffplug.spotless'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	spotless {
		java {
			googleJavaFormat("1.15.0")
			removeUnusedImports()
		}
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-amqp'
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-validation'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'org.springframework.amqp:spring-rabbit-test'

		implementation 'org.springdoc:springdoc-openapi-starter-webmvc-api:2.0.2'
		implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
